импорт ОбщегоНазначения

@ВПроекте @НаСервере
метод ДобавитьВоду(ОбъемВыпитойВоды = 250)
    пер Запись = новый ОбъемВыпитого.Запись(Период = ДатаВремя.Сейчас(), Объем = ОбъемВыпитойВоды)
    ОбъемВыпитого.Записать(Запись)
;

@ВПроекте @НаСервере
метод УдалитьПоследнююЗапись()
    знч ПериодПоследнейЗаписи = ПериодПоследнейЗаписи()
    знч КлючЗаписи = новый ОбъемВыпитого.КлючЗаписи(Период = ПериодПоследнейЗаписи)
    ОбъемВыпитого.Удалить(КлючЗаписи)
;

@ВПроекте @НаСервере
метод ВыпитоЗаДеньВсего(ПериодПолучения: ДатаВремя, ВЛитрах = Ложь): Число
    знч НачалоДня = ПериодПолучения.НачалоДня()
    знч КонецДня = ПериодПолучения.КонецДня()

    знч Запрос = Запрос{
        ВЫБРАТЬ
            СУММА(ОбъемВыпитого.Объем) КАК Объем
        ИЗ
            ОбъемВыпитого КАК ОбъемВыпитого
        ГДЕ
            ОбъемВыпитого.Период МЕЖДУ %НачалоДня И %КонецДня
    }

    пер Выпито = Запрос.Выполнить().ПервыйИлиУмолчание()?.Объем как Число
    если ВЛитрах 
        Выпито /= 1000
    ;

    возврат Выпито
;

@ВПроекте @НаСервере
метод ВыпитоЗаДеньСтрокой(ПериодПолучения: ДатаВремя): Строка
    знч НачалоДня = ПериодПолучения.НачалоДня()
    знч КонецДня = ПериодПолучения.КонецДня()

    знч Запрос = Запрос{
        ВЫБРАТЬ
            ОбъемВыпитого.Период КАК Период,
            ОбъемВыпитого.Объем КАК Объем
        ИЗ
            ОбъемВыпитого КАК ОбъемВыпитого
        ГДЕ
            ОбъемВыпитого.Период МЕЖДУ %НачалоДня И %КонецДня
        УПОРЯДОЧИТЬ ПО Период
    }

    исп РезультатЗапроса = Запрос.Выполнить()
    
    пер Всего = 0
    пер СтрокиОтчета = новый Массив<Строка>()
    
    для СтрокаРезультата из РезультатЗапроса
        знч ДатаФормат = СтрокаРезультата.Период.Время
        знч ВыпитоВоды = СтрокаРезультата.Объем
        СтрокиОтчета.Добавить("$ВыпитоВоды мл. - $ДатаФормат")
        Всего += ВыпитоВоды
    ;

    Всего /= 1000
    знч ВсегоЛитров = СтроковыеФункции.СтрокаСЧислительным(Всего, "литр", "литра", "литров")
    СтрокиОтчета.Вставить(0, "Всего выпито $ВсегоЛитров. По времени:")
    
    возврат Строки.Соединить(СтрокиОтчета, Символы.НОВАЯ_СТРОКА)

;

@ВПроекте @НаСервере
метод ПериодПоследнейЗаписи(): ДатаВремя
    знч ДатаСреза = ДатаВремя.Сейчас()
    знч Запрос = Запрос{
        ВЫБРАТЬ
            ОбъемВыпитого.Период КАК Период
        ИЗ
            ОбъемВыпитого.СрезПоследних(%ДатаСреза) КАК ОбъемВыпитого
    }

    возврат Запрос.Выполнить().ПервыйИлиУмолчание()?.Период
;
