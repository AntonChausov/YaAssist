импорт Интеграция
импорт ОбщегоНазначения

@ВПроекте @НаСервере
метод ОбработатьКомандуИнтеграции(Система: Системы, Команда: Строка, СловаКоманды:Массив<Строка>): Строка
    
    пер ТекстВОтвет = ""
    если ЭтоКомандаДобавленияВоды(СловаКоманды)
        знч Количество = КоличествоВодыПоКоманде(СловаКоманды)
        ОбъемВыпитого.ДобавитьВоду(Количество)
        знч КоличествоСтрокой = СтроковыеФункции.СтрокаСЧислительным(Количество, "миллилитр", "миллилитра", "миллилитров")
        ТекстВОтвет = "$КоличествоСтрокой, добавлено"
    иначе если ЭтоКомандаСказатьСколькоВоды(СловаКоманды)
        знч Количество = ОбъемВыпитого.ВыпитоЗаДеньВсего(ДатаВремя.Сейчас(), Истина)
        знч КоличествоСтрокой = СтроковыеФункции.СтрокаСЧислительным(Количество, "лилитр", "лилитра", "лилитров")
        ТекстВОтвет = "Выпито $КоличествоСтрокой"
    иначе если ЭтоКомандаУдалитьВоду(СловаКоманды)
        ОбъемВыпитого.УдалитьПоследнююЗапись()
        ТекстВОтвет = "Удалено"
    иначе если ЭтоКомандаОтчетВТГПоВоде(СловаКоманды)
        знч Отчет = ОбъемВыпитого.ВыпитоЗаДеньСтрокой(ДатаВремя.Сейчас())
        если Система == Системы.Алиса
            Интеграция.ОтправитьВТелеграмм(Отчет, Неопределено)
            ТекстВОтвет = "Отправлено"
        иначе
            ТекстВОтвет = Отчет
        ;
    иначе
        ТекстВОтвет = "Некорректно сформулирована команда для учета воды"
    ;
    
    возврат ТекстВОтвет
;

@Локально @НаСервере
метод ЭтоКомандаДобавленияВоды(СловаКоманды: Массив<Строка>): Булево
    знч СловаДолжныБыть = ["добавь", "добавить"]
    возврат УниверсальныеКоллекции.МассивыПересекаются(СловаКоманды, СловаДолжныБыть)
;

@Локально @НаСервере
метод КоличествоВодыПоКоманде(СловаКоманды: Массив<Строка>): Число
    пер Количество = 0

    пер Умножать = КоличествоВЛитрах(СловаКоманды)

     для Слово из СловаКоманды
        если Слово.ТолькоЦифры()
            Количество = новый Число(Слово)
            прервать
        иначе если Слово == "пол"
            Количество = 0.5
        ;
    ;

    если Умножать
        Количество = Макс(1, Количество) * 1000
    ;
    
    если Количество == 0
        знч КоличествоВодыПоУмолчанию = Настройки.НастройкаПоИмени("КоличествоВоды", "250")
        Количество = новый Число(КоличествоВодыПоУмолчанию)
    ;

    возврат Количество
;

@Локально @НаСервере
метод КоличествоВЛитрах(СловаКоманды: Массив<Строка>): Булево
    знч СловаДолжныБыть = ["литр", "литра", "литров"]
    возврат УниверсальныеКоллекции.МассивыПересекаются(СловаКоманды, СловаДолжныБыть)
;

@Локально @НаСервере
метод ЭтоКомандаСказатьСколькоВоды(СловаКоманды: Массив<Строка>): Булево
    знч СловаДолжныБыть = ["сколько", "количество"]
    возврат УниверсальныеКоллекции.МассивыПересекаются(СловаКоманды, СловаДолжныБыть)
;

@Локально @НаСервере
метод ЭтоКомандаУдалитьВоду(СловаКоманды: Массив<Строка>): Булево
    знч СловаДолжныБыть = ["удали", "удалить", "отменить"]
    возврат УниверсальныеКоллекции.МассивыПересекаются(СловаКоманды, СловаДолжныБыть)
;

@Локально @НаСервере
метод ЭтоКомандаОтчетВТГПоВоде(СловаКоманды: Массив<Строка>): Булево
    знч СловаДолжныБыть = ["отчет", "детали", "информация"]
    возврат УниверсальныеКоллекции.МассивыПересекаются(СловаКоманды, СловаДолжныБыть)
;

