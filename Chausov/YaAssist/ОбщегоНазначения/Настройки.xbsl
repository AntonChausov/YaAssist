
@ВПроекте @НаСервере
метод НастройкаПоИмени(ИмяНастройки:Строка, ЗначениеПоУмолчанию: Строка): Строка
    
    знч ДатаСреза = ДатаВремя.Сейчас()
    знч Запрос = Запрос{
        ВЫБРАТЬ ПЕРВЫЕ 1
            Настройки.Значение КАК Значение
        ИЗ
            Настройки.СрезПоследних(%ДатаСреза) КАК Настройки
        ГДЕ
            Настройки.Настройка == %ИмяНастройки
    }

    знч РезультатЗапроса = Запрос.Выполнить().ПервыйИлиУмолчание()?.Значение
    если РезультатЗапроса != Неопределено
        возврат РезультатЗапроса
    ;
    возврат ЗначениеПоУмолчанию
;

@ВПроекте @НаСервере
метод НастройкиПоИменам(ИменаНастроек: Массив<Строка>): Соответствие<Строка, Строка>
    знч ДатаСреза = ДатаВремя.Сейчас()
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Настройки.Настройка КАК Настройка,
            Настройки.Значение КАК Значение
        ИЗ
            Настройки.СрезПоследних(%ДатаСреза) КАК Настройки
        ГДЕ
            Настройки.Настройка В (%ИменаНастроек)
    }

    пер Результат = новый Соответствие<Строка, Строка>()
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        Результат.Вставить(СтрокаРезультата.Настройка, СтрокаРезультата.Значение)
    ;
    
    для Настройка из ИменаНастроек
        если Результат.ПолучитьИлиУмолчание(Настройка) == Неопределено
            Результат.Вставить(Настройка, "")
        ;
    ;
    
    возврат Результат
;